{
	tavuta := localImportNamespace("tavuttaja.röd", dir=SOURCE_DIR).tavuta
}

teeMarkov(aineisto, tietokanta=new map) {
	errprint("Luodaan tietokantaa...\n")
	i := 0
	readLines(aineisto) | lowerCase(_) | replace("[^a-zåäö ]", "") | split() | slide(2) | for sana1, sana2 do
		errprint("\rn = ", i)
		i ++
		tietokanta[sana1] = [] unless [ tietokanta[sana1]? ]
		tietokanta[sana1] += sana2
	done
	errprint("\rAineisto luotu!\n")
	return tietokanta
}

teeLausetietokanta(aineisto, tietokanta=new map) {
	errprint("Luodaan tietokantaa...\n")
	i := 0
	v := "aeiouyäöå"
	k := "bcdfghjklmnpqrstvwx"
	readLines(aineisto) | lowerCase(_) | split(sep="[.!?]") | replace("[^a-zåäö ]", "", "^\\s+|\\s+$", "", "\\s+", " ") | for lause do
		errprint("\rn = ", i)
		i ++
		tavuja := #splitMany(lause, sep=`(?<=[$v$k])(?=[$k][$v])|(?<=[$v&&[^a]])(?=[a])|(?<=[$v&&[^o]])(?=[o])|\s+`)
		tietokanta[tavuja] = [] unless [ tietokanta[tavuja]? ]
		tietokanta[tavuja] += lause
	done
	errprint("\rAineisto luotu!\n")
	return tietokanta
}
