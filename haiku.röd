{
	tavuta := localImportNamespace("tavuttaja.röd", dir=SOURCE_DIR).tavuta
	markov := localImportNamespace("markov.röd", dir=SOURCE_DIR)
	teeMarkov := markov.teeMarkov
	teeLausetietokanta := markov.teeLausetietokanta
}

valitse(lista) {
	return lista[abs(randomInteger()%#lista)]
}

teeSäe(n, tietokanta, alku="") {
	sanat := []
	
	errprint("Luodaan säettä (n = ", n, ")...\n")
	
	while [ #[sanat() | tavuta(_)] < n ] do
		errprint("\r", sanat&" ")
		if [ #sanat = 0 ] do
			if [ alku = "" ] do
				sanat += valitse([keys(tietokanta)])
			else
				sanat += alku
			done
		else
			sanat += valitse(tietokanta[sanat[-1]])
		done
		del sanat[:] if [ #[sanat() | tavuta(_)] > n ]
	done
	
	errprint("\rSäe luotu!\n")
	
	return sanat&" "
}

main(tyyppi, aineisto, n, s...) {
	n = parseInteger(n)
	if [ tyyppi = "markov" ] do
		tietokanta := teeMarkov(aineisto)
		push({ |n|; teeSäe(n, tietokanta, alku="") })
	else
		tietokanta := teeLausetietokanta(aineisto)
		push({ |n|; valitse(tietokanta[n]) })
	done | pull(säe)
	seq(1, n) | for i do
		[5, 7, 5] if [ #s = 0 ] else s() | print(säe(_))
		print("") unless [ i = n ]
	done
}
